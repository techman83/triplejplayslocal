package App::TriplejPlaysLocal;

use 5.010;
use Method::Signatures 20140224;
use App::TriplejPlaysLocal::Twitter;
use App::TriplejPlaysLocal::Tweets;
use AnyEvent;
use EV;
use Moo;
use namespace::clean;

# ABSTRACT: Time Shifts @triplejplays

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

    use App::TriplejPlaysLocal;

    my $plays = App::TriplejPlaysLocal->new();

=head1 DESCRIPTION

Main App!

=cut

our $DEBUG = $ENV{TRIPLEJ_DEBUG} || 0;

has '_tweets'   => ( is => 'rw', lazy => 1, builder => 1 );
has '_twitter'  => ( is => 'rw', lazy => 1, builder => 1 );

method _build__twitter {
  return App::TriplejPlaysLocal::Twitter->new(
    tweets => $self->_tweets,
  );
}

method _build__tweets {
  return App::TriplejPlaysLocal::Tweets->new();
}

method get_tweets {
  $self->debug("Getting tweets");
  $self->_twitter->get_tweets;
}

method check_tweets {
  $self->debug("Checking Tweets");
  my $index = $self->_tweets->check_time;
  if ($index != -1) {
    my $tweet = $self->_tweets->get_tweet($index)->build_tweet;
    $self->info("Tweeting: $tweet");
    $self->_twitter->tweet($tweet) unless $DEBUG;
    $self->_tweets->delete_tweet($index);
  }
}

method run {
  my $get_tweets = AE::timer 0, 60, sub { $self->get_tweets; };
  my $check_tweets = AE::timer 30, 60, sub { $self->check_tweets; };
  # Write a loop to run periodically to clean out old tweets.
  EV::loop;
}

with('App::TriplejPlaysLocal::Logger');

1;
