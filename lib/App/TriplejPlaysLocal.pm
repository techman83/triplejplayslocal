package App::TriplejPlaysLocal;

use 5.010;
use Method::Signatures 20140224;
use App::TriplejPlaysLocal::Twitter;
use App::TriplejPlaysLocal::Tweets;
use AnyEvent;
use EV;
use Moo;
use namespace::clean;

# ABSTRACT: Time Shifts @triplejplays

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

    use App::TriplejPlaysLocal;

    my $plays = App::TriplejPlaysLocal->new();
    
    $plays->run;

=head1 DESCRIPTION

This ties the TriplejPlaysLocal app together. Calling '->run' will 
cause it to get/tweet periodically until terminated.

=cut

our $DEBUG = $ENV{TRIPLEJ_DEBUG} || 0;

has '_tweets'   => ( is => 'rw', lazy => 1, builder => 1 );
has '_twitter'  => ( is => 'rw', lazy => 1, builder => 1 );

method _build__twitter {
  return App::TriplejPlaysLocal::Twitter->new(
    tweets => $self->_tweets,
  );
}

method _build__tweets {
  return App::TriplejPlaysLocal::Tweets->new();
}

=method get_tweets

  $plays->get_tweets;

Calls the Twitter object to fetch tweets from @triplejplays.

=cut

method get_tweets {
  $self->debug("Getting tweets");
  $self->_twitter->get_tweets;
}

=method check_tweets

  $plays->check_tweets;

Checks if there is a song for the current minutes and tweets it. 
After which it will delete the song from the tweets object.

=cut

method check_tweets {
  $self->debug("Checking Tweets");
  my $index = $self->_tweets->check_time;
  if ($index != -1) {
    my $tweet = $self->_tweets->get_tweet($index)->build_tweet;
    $self->info("Tweeting: $tweet");
    $self->_twitter->tweet($tweet) unless $DEBUG;
    $self->_tweets->delete_tweet($index);
  }
}

=method expire_tweets

  $plays->expire_tweets;

Checks if there are any tweets older than 21600 seconds (5 hours) and
purges them.

=cut

method expire_tweets {
  $self->debug("Expiring Tweets");
  my @expired = $self->_tweets->expired_tweets;
  foreach my $index (@expired) {
    my $tweet = $self->_tweets->get_tweet($index)->build_tweet;
    $self->info("Expiring: $tweet");
    $self->_tweets->delete_tweet($index);
  }
}

=method run

  $plays->run;

Sets up the AnyEvent timers to get new tweets every 10 minutes and 
check if there is a song to tweet every minute.

=cut

method run {
  my $get_tweets = AE::timer 0, 600, sub { $self->get_tweets; };
  my $expire_tweets = AE::timer 15, 3600, sub { $self->expire_tweets; };
  my $check_tweets = AE::timer 30, 60, sub { $self->check_tweets; };
  EV::loop;
}

with('App::TriplejPlaysLocal::Logger');

1;
