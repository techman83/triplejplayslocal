package App::TriplejPlaysLocal::Song;

use v5.010;
use autodie;
use Carp qw(croak);
use Method::Signatures;
use Moo;
use namespace::clean;

# ABSTRACT: Song Object

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

    use App::TriplejPlaysLocal::Song;

    my $song = App::TriplejPlaysLocal::Song->new(
      id      => '1234',
      tweet   => 'Artist - Song [time]',
    );

=head1 DESCRIPTION

A Tweet.

=cut

has 'id'      => ( is => 'ro', required => 1 );
has 'tweet'   => ( is => 'ro', required => 1 );
has '_tweet'  => ( is => 'ro', lazy => 1, builder => 1 );
has 'artist'  => ( is => 'ro', lazy => 1, builder => 1 );
has 'track'   => ( is => 'ro', lazy => 1, builder => 1 );
has 'time'    => ( is => 'ro', lazy => 1, builder => 1 );

method _build__tweet {
  my $tweet = $self->{tweet};
  $tweet =~ s/\@(\w+)/$1/g;
  return $tweet;
}

method _build_time {
  $self->_tweet =~ m{\[(?<time>\d+:\d+)\]$}x;
  return $+{'time'};
}

method _build_track {
  $self->_tweet =~ m{\s-\s(?<track>.+).\[}x;
  return $+{track};
}

method _build_artist {
  $self->_tweet =~ m{^\.?(?<artist>.+)\s-}x;
  return $+{artist};
}

method build_tweet {
  return $self->artist." - ".$self->track." [".$self->time."]";
}

1;
